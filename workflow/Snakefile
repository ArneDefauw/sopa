from utils import WorkflowPaths, Args

paths = WorkflowPaths(config)
args = Args(paths, config)

rule all:
    input:
        paths.annotations,
        paths.explorer_experiment,
        paths.explorer_image,

rule to_spatialdata:
    input:
        paths.data_path,
    output:
        paths.sdata_zgroup if paths.data_path else [],
    conda:
        "spatial"
    params:
        args_reader = str(args['read'])
    shell:
        """
        sopa read {paths.data_path} --sdata-path {paths.sdata_path} {params.args_reader}
        """

checkpoint patchify_cellpose:
    input:
        paths.sdata_zgroup,
    output:
        n_patches = paths.smk_n_patches_cellpose,
        patches = touch(paths.smk_patches),
    params:
        args_patchify = str(args["patchify"].where(contains="pixel")),
    conda:
        "spatial"
    shell:
        """
        sopa patchify cellpose {paths.sdata_path} {params.args_patchify}
        """

checkpoint patchify_baysor:
    input:
        paths.sdata_zgroup,
        paths.smk_cellpose_boundaries if args.cellpose else [],
    output:
        n_patches = paths.smk_n_patches_baysor,
        baysor_temp_dir = directory(paths.baysor_temp_dir),
    params:
        args_patchify = str(args["patchify"].where(contains="micron")),
        args_baysor = args.dump_baysor_patchify() if args.baysor else "",
        arg_prior = "--use-prior" if args.cellpose else ""
    conda:
        "spatial"
    shell:
        """
        sopa patchify baysor {paths.sdata_path} {params.args_patchify} {params.args_baysor} {params.arg_prior}
        """

rule patch_segmentation_cellpose:
    input:
        paths.smk_n_patches_cellpose,
        paths.smk_patches,
    output:
        f"{paths.cellpose_temp_dir}/{{index}}.zarr.zip",
    conda:
        "spatial"
    params:
        args_cellpose = str(args["segmentation"]["cellpose"]),
    shell:
        """
        sopa segmentation cellpose {paths.sdata_path} --patch-dir {paths.cellpose_temp_dir} --patch-index {wildcards.index} {params.args_cellpose}
        """

rule patch_segmentation_baysor:
    input:
        n_patches = paths.smk_n_patches_baysor,
        baysor_patch = f"{paths.baysor_temp_dir}/{{index}}",
    output:
        f"{paths.baysor_temp_dir}/{{index}}/segmentation_polygons.json"
    params:
        args_baysor_cell_key = args.baysor_cell_key,
    resources:
        mem_mb=64_000,
    shell:
        """
        if command -v module &> /dev/null; then
            module purge
        fi
        
        cd {input.baysor_patch}
        {config[executables][baysor]} run --save-polygons GeoJSON -c config.toml transcripts.csv {params.args_baysor_cell_key}
        """

def get_input_resolve(name):
    def _(wilcards):
        with getattr(checkpoints, f"patchify_{name}").get(**wilcards).output.n_patches.open() as f:
            return paths.cells_paths(int(f.read()), name)
    return _

rule resolve_cellpose:
    input:
        get_input_resolve("cellpose"),
    output:
        touch(paths.smk_cellpose_boundaries),
    conda:
        "spatial"
    params:
        args_expand_radius = args.expand_radius,
    shell:
        """
        sopa resolve cellpose {paths.sdata_path} --patch-dir {paths.cellpose_temp_dir} --expand-radius {params.args_expand_radius}
        """

rule resolve_baysor:
    input:
        files = get_input_resolve("baysor"),
    output:
        touch(paths.smk_baysor_boundaries),
        touch(paths.smk_table)
    conda:
        "spatial"
    params:
        n_patches = lambda wildcards, input: len(input.files),
    shell:
        """
        sopa resolve baysor {paths.sdata_path} --baysor-temp-dir {paths.baysor_temp_dir} --gene-column {args.gene_column} --n {params.n_patches}
        """

rule aggregate:
    input:
        paths.smk_baysor_boundaries if args.baysor else paths.smk_cellpose_boundaries,
    output:
        touch(paths.smk_aggregation),
    conda:
        "spatial"
    params:
        args_aggregate = str(args["aggregate"]),
    shell:
        """
        sopa aggregate {paths.sdata_path} {params.args_aggregate}
        """

rule annotate:
    input:
        paths.smk_table if not args.aggregate else paths.smk_aggregation,
    output:
        directory(paths.annotations),
    conda:
        "spatial"
    params:
        args_fluorescence = str(args["annotation"]["fluorescence"])
    shell:
        """
        sopa annotate fluorescence {paths.sdata_path} {params.args_fluorescence}
        """

rule image_write:
    input:
        paths.sdata_zgroup,
    output:
        paths.explorer_image,
    conda:
        "spatial"
    resources:
        mem_mb=64_000,
    params:
        args_explorer = str(args["explorer"].where(keys=['lazy', 'ram_threshold'])),
    shell:
        """
        sopa explorer {paths.sdata_path} {paths.explorer_directory} {params.args_explorer} --save-image-mode 2
        """

rule explorer:
    input:
        paths.smk_baysor_boundaries if args.baysor else paths.smk_cellpose_boundaries,
        paths.smk_aggregation if args.aggregate else paths.smk_table,
        paths.annotations if args.annotate else [],
    output:
        paths.explorer_experiment,
    conda:
        "spatial"
    params:
        args_explorer = str(args["explorer"]),
    shell:
        """
        sopa explorer {paths.sdata_path} {paths.explorer_directory} {params.args_explorer} --save-image-mode 0
        """